;sfnoesyhmqc3d.HnHaHm-CmHm: sofast 3D NOESY-HMQC for HnHaHm(F1)-Cm(F2)Hm(F3)
;HnHaHm(t1) -NOE -H-C(t2)-H(t3): HnHaHm(t1) for Hn, Haro & Hm, C(t2) and H(t3) are for methyl
;Youlin Xia on 05/05/2016

 
/*
sw(F1) may be 5 ppm
TD(F1) may be 256  (128 for each 3D)

use "split ipap 2" to split the data and enter 0 for question "splitting in F1(0) or F2(1)"

two sets of 3D data are acquired, one is for HnHa(t1) + Hm(t1) and the other is for HnHa(t1) - Hm(t1)
two sub 3D spectra will be obtained:

1: HnHaro(t1) NOE Cm(t2)-Hm(t3)
2: Hm(t1)     NOE Cm(t2)-Hm(t3)

*/


;$CLASS=HighRes
;$DIM=3D
;$TYPE=
;$SUBTYPE=
;$COMMENT=

prosol relations=<triple>

#include <Avance.incl>
#include <Delay.incl>
#include <Grad.incl>

"d11=30m"


"in0 =inf1/2"
"in10=inf2/2"
"d0=3u"
"d10=0u"
"d14=1s/(cnst4*2)"	

"TAU=d8-p3-p16*3-0.5m" 	

"p41=7.2/(cnst2*bf1/1000000)" /*  PC9  pulse length  */
"p42=4.875/(cnst2*bf1/1000000)" /* REBURP pulse length  */

	;"spw29=4*plw1*(pow((p1/p41)/0.125,2))" /* PC9  power level  */					;cosine-modulated
"sp29=pl1-20*log(2*(p1/p41)/0.125)/log(10)" /* PC9  power level  */
"spoff29=0"  /*  PC9  offset */

	;"spw30=4*plw1*(pow((p1/p41)/0.125,2))" /* PC9  power level  */					;sine-modulated
"sp30=pl1-20*log(2*(p1/p41)/0.125)/log(10)" /* PC9  power level  */
"spoff30=0"  /*  PC9  offset */



	;"spw27=plw1*(pow((p1*(cnst3/90)/p41)/0.125,2))"
"sp27=pl1-20*log((cnst3/90.0)*(p1/p41)/0.125)/log(10)" /* PC9  power level  */
"spoff27=bf1*(cnst1/1000000)-o1"  /*  PC9  offset */

	;"spw28=plw1*(pow((p1*2/p42)/0.0798,2))"
"sp28=pl1-20*log((p1*2/p42)/0.0798)/log(10)"   /* REBURP power level  */
"spoff28=spoff27"  		/*  PC9  offset */

"DELTA =0.000028*850*1000000/bf1" 	;28us, adjustable delay for zero first order phase in F3 dimension
"DELTA1=d14-p16-d16-p41*0.5"
"DELTA2=p41*0.5-de-4u"

"l0=0"

#define w3 (p1*0.231 ph7 d19*2 p1*0.692 ph7 d19*2 p1*1.462 ph7 d19*2 p1*1.462 ph8 d19*2 p1*0.692 ph8 d19*2 p1*0.231 ph8):f1 
"d44=d19*2-(p1*0.087 + p1*0.206)*0.5"
"d43=d19*2-(p1*0.206 + p1*0.413)*0.5"
"d42=d19*2-(p1*0.413 + p1*0.778)*0.5"
"d41=d19*2-(p1*0.778 + p1*1.491)*0.5"
"d40=d19*2-(p1*1.491 + p1*1.491)*0.5"
#define w5p (p1*0.087 ph7 d44 p1*0.206 ph7 d43 p1*0.413 ph7 d42 p1*0.778 ph7 d41 p1*1.491 ph7 d40 p1*1.491 ph8 d41 p1*0.778 ph8 d42 p1*0.413 ph8 d43 p1*0.206 ph8 d44 p1*0.087 ph8):f1

aqseq 312


1 d11 ze
2 d11 do:f2
3 d1 pl0:f1 pl12:f2 pl3:f3

  10u UNBLKGRAD
  
  "d21 = d10*2 - p3*0.637*2"
  "if (d21 < 0) { d21 = 0; }"

  (p21 ph2):f3
  p16:gp3*1.3
  d16
  
  ;NOESY
  5u cpds2:f2
  if "l0 % 2 == 0" 
  {
    (p41:sp29 ph3):f1		;cosine-modulated
  }
  else
  {
    (p41:sp30 ph4):f1		;sine-modulated
  }


  d0 
  (p22 ph1):f3
  d0 pl1:f1
  w3
  3u  
  (p22 ph1):f3
  3u pl0:f1 
  (p41:sp29 ph1):f1
  4u do:f2 
  
  ;mixing
  4u
  TAU pl2:f2  
  p16*3:gp3
  0.5m
    
  ;HMQC2
  
  (p41:sp27 ph1):f1
  p16:gp1
  d16

  (center (p42:sp28 ph1):f1 (DELTA1 p3 ph6 d21 p3 ph1 DELTA1):f2 )


  DELTA2
  p16:gp1
  d16 pl12:f2
  4u BLKGRAD
  DELTA
    
  go=2 ph31 cpd2:f2
  d11 do:f2 mc #0 to 2 
     ;for topspin2.0
     F1I(iu0, 2)
     F1PH(ip3 & ip4, id0) 
     F2PH(rd0 & rp3 & rp4 & ip6, id10)

     ;for topspin3.0
     ;F1I(iu0, 2)
     ;F1PH(calph(ph3, +90), caldel(d0, +in0)) 
     ;F2PH(calph(ph6, +90), caldel(d10, +in10)) 
exit


ph1 =0
ph2 =1
ph3 =0 2
ph4 =1 3
ph6 =0 0 2 2
ph7 =0 0 0 0 2 2 2 2
ph8 =2 2 2 2 0 0 0 0
ph31=0 2 2 0



;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;sp27: f1 channel - power level for shaped  90 degree pulse
;sp28: f1 channel - power level for shaped 180 degree pulse
;spnam27: Pc9_4_90.1000
;spnam28: Reburp.1000
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p16: homospoil/gradient pulse                         [1 msec]
;p41: f1 channel -   90 degree shaped pulse
;p42: f1 channel -  180 degree shaped pulse
;d0 : incremented delay (F1 in 3D)                     [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d2 : 1/((2J)XH)
;d8 : mixing time
;d10: incremented delay (F2 in 3D)                     [3 usec]
;d11: delay for disk I/O                               [30 msec]
;d14: 1/(2JCH)                                         [3.7 msec]
;d16: delay for homospoil/gradient recovery
;d21: run time delay given by the pulse sequence
;cnst1: center of methyl 1H in ppm [0.9]
;cnst2: 1H excitation bandwidth [5]
;cnst3: flipping angle of the 2nd HMQC[110]
;cnst4: 1JCH of methyl [125]
;o1p:    center of methyl 1H  chemical shifts [4.7]
;o2p:    center of methyl 13C chemical shifts [16]

;inf1: 1/SW(H) = 2 * DW(H)
;inf2: 1/SW(X) = 2 * DW(X)
;in0: 1/(2 * SW(H)) = DW(H)
;nd0: 2
;in10: 1/(2 * SW(X)) = DW(X)
;nd10: 2
;NS: 8 * n
;DS: 8
;td1: number of experiments in F1
;td2: number of experiments in F2
;FnMODE: States-TPPI (or TPPI) in F1
;FnMODE: States-TPPI (or TPPI) in F2
;cpd2: decoupling according to sequence defined by cpdprg2
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;cpdprg2: f2 channel - bi_garp_2pl


;for z-only gradients:
;gpz1:  25%
;gpz3:  30%

;use gradient files:   
;gpnam1: SMSQ10.100
;gpnam3: SMSQ10.100

