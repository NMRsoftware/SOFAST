;sfhmqc.aro: sofast 2D HMQC for aromatic ring
;Youlin Xia on 05/05/2016

;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=

prosol relations=<triple>

#include <Avance.incl>
#include <Delay.incl>
#include <Grad.incl>

"d11=30m"

"in0 =inf1/2"
"d0=0u"

"d15=1s/(cnst5*2)"	;cnst5=160

"p41=7.2/(cnst2*bf1/1000000)" /*  PC9  pulse length  */
"p42=4.875/(cnst2*bf1/1000000)" /* REBURP pulse length  */

	;"spw27=plw1*(pow((p1*(cnst3/90)/p41)/0.125,2))" /* PC9  power level  */
"sp27=pl1-20*log((cnst3/90.0)*(p1/p41)/0.125)/log(10)" /* PC9  power level  */
"spoffs27=bf1*(cnst21/1000000)-o1"  		/*  PC9  offset */

	;"spw28=plw1*(pow((p1*2/p42)/0.0798,2))"   /* REBURP power level  */
"sp28=pl1-20*log((p1*2/p42)/0.0798)/log(10)"   /* REBURP power level  */
"spoffs28=spoffs27"			 /* REBURP offset */

"DELTA1=d15-p16-d16-p41*0.5"
"DELTA2=p41*0.5"

1 d11 ze
2 d11 do:f2
3 d1 pl0:f1 pl2:f2

  10u UNBLKGRAD 
  (p3 ph1):f2
  p16:gp3
  200u

  "d21 = d0*2 - p3*0.637*2"
  "if (d21 < 0) { d21 = 0; }"
  
  ;HMQC1
  (p41:sp27 ph1):f1
  p16:gp1
  d16

  (center (p42:sp28 ph5):f1 (DELTA1 p3 ph3 d21 p3 ph4 DELTA1):f2 )

  p16:gp1
  d16 pl12:f2
  DELTA2  BLKGRAD
   
  go=2 ph31 cpd2:f2
  d11 do:f2 mc #0 to 2 
     F1PH(ip3, id0) 
exit


ph1 =0
ph2 =1
ph3 =0 2
ph4 =0 0 2 2
ph5 =0 0 0 0 1 1 1 1
ph31=0 2 2 0 2 0 0 2


;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;sp27: f1 channel - power level for shaped  90 degree pulse 
;sp28: f1 channel - power level for shaped 180 degree pulse
;spnam27: Pc9_4_90.1000
;spnam28: Reburp.1000
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p16: homospoil/gradient pulse                         [1 msec]
;p41: f1 channel -   90 degree shaped pulse
;p42: f1 channel -  180 degree shaped pulse
;d0 : incremented delay (F1 in 3D)                     [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d2 : 1/((2J)XH)
;d8 : mixing time
;d10: incremented delay (F2 in 3D)                     [3 usec]
;d11: delay for disk I/O                               [30 msec]
;d14: 1/(2JCH)                                         [3.7 msec]
;d16: delay for homospoil/gradient recovery
;d20: run time delay given by the pulse sequence
;cnst2: 1H excitation bandwidth [5 ppm]
;cnst3: 1H flip angle [120]
;cnst5: 1JCH of aromatic ring [160]
;cnst21: center of aromatic 1H  chemical shifts [8.5]
;o1p:    center of methyl 1H  chemical shifts [4.7]
;o2p:    center of methyl 13C chemical shifts [125]

;inf1: 1/SW(H) = 2 * DW(H)
;in0: 1/(2 * SW(H)) = DW(H)
;nd0: 2
;NS: 4 * n
;DS: 16
;td1: number of experiments in F1
;FnMODE: States-TPPI (or TPPI) in F1
;cpd2: decoupling according to sequence defined by cpdprg2
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;cpdprg2: f2 channel - bi_garp_2pl


;for z-only gradients:
;gpz1: 11%
;gpz2:  7%
;gpz3: 25%

;use gradient files:   
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100
;gpnam3: SMSQ10.100

