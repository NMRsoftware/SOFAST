;sfCNhmqcgpph19: sofast simultaneous 2D 15N/13C HMQC for HN and methyl
;Youlin Xia on 05/05/2016

;3D SOFAST-NOESY delivers high sensitivity and resolution to protein NMR.
;P. Rossi, Y. Xia, N. Khanra, G. Veglia, and C.G. Kalodimos	 
;Accepted by JBNMR (2016)

;$CLASS=HighRes
;$DIM=3D
;$TYPE=
;$SUBTYPE=
;$COMMENT=

prosol relations=<triple>

#include <Avance.incl>
#include <Delay.incl>
#include <Grad.incl>

"p4=p3*2"
"p19=300"
"p22=p21*2"
"d11=30m"
"d13=4u"
"d14=4.0m"   ;for 13C
"d15=5.2m"   ;for 15N

"d0=0u"
"d10=0u"
"in0 =inf1/2"		;cnst8 must be less than swC
"in10=1s/(2*cnst8)"   	;cnst8 = 30 * sfo3, 30 ppm of spectral width for 15N

"l1 = (d14-(1-0.638)*(p21-p3) -p19 -d16 -14u)*2/(in10-in0)+1"	;TD(F1) <= L1
"TAU1 = l1*0.1"	;to show L1

"cnst2=5.0"
"p41=7.2/(cnst2*bf1/1000000)" /*  PC9  pulse length  */

"spw31=4*plw1*(pow((cnst3/90.0)*(p1/p41)/0.125,2))" /* PC9  power level  */
	;"sp31=pl1-20*log(2*(p1/p41)/0.125)/log(10)" /* PC9  power level  */
"spoff31=0"  /*  PC9  offset */

"DELTA=p4"

#define w3 (p1*0.231 ph7 d19*2 p1*0.692 ph7 d19*2 p1*1.462 ph7 d19*2 p1*1.462 ph8 d19*2 p1*0.692 ph8 d19*2 p1*0.231 ph8):f1 
"d44=d19*2-(p1*0.087 + p1*0.206)*0.5"
"d43=d19*2-(p1*0.206 + p1*0.413)*0.5"
"d42=d19*2-(p1*0.413 + p1*0.778)*0.5"
"d41=d19*2-(p1*0.778 + p1*1.491)*0.5"
"d40=d19*2-(p1*1.491 + p1*1.491)*0.5"
#define w5p (p1*0.087 ph7 d44 p1*0.206 ph7 d43 p1*0.413 ph7 d42 p1*0.778 ph7 d41 p1*1.491 ph7 d40 p1*1.491 ph8 d41 p1*0.778 ph8 d42 p1*0.413 ph8 d43 p1*0.206 ph8 d44 p1*0.087 ph8):f1


1 d11 ze
2 d11 do:f2 do:f3
3 d1 pl0:f1 pl12:f2 pl3:f3 BLKGRAD
  10u UNBLKGRAD


  "d32 = d0*2 - p3*0.637*2"
  if "d32 < 0"
   { 
   "d32 = 0"
   }
  "d33 = d10*2 - p21*0.637*2"
  if "d33 < 0"
   { 
   "d33 = 0"
   }

  "d34 = d15-d14 + 0.5*(p22-p4+d33-d32)"	;d34 > 0.5*p41
  "d35 = d34-p41*0.5"
  "d36 = d14-p19-d16-14u"
  "d37 = d15 -d34 - p19- d16 -14u"			;d37 should be > 0   
  if "d32 == 0"
   { 
   "d37=d36-p21"
   "d38=d37-p22-p4"
   }	;1st point

  (p21 ph2):f3
  p16:gp1
  200u


  4u cpd2:f2
  (p41:sp31 ph1):f1 
  d35
  4u do:f2
   		     
  10u
  p19:gp2 
  d16 pl1:f1 pl2:f2

        	     
  if "d32 > 0"
  {
  (center w3 (d36 p3 ph5 d32 p3 ph1 d36):f2 (d37 p21 ph5 d33 p21 ph8 d37):f3)
  }
  else
  {
  (center w3 (d36 p3 ph5 d32 p3 ph1 d36):f2 (d37 p21 ph5 DELTA p22 ph2 DELTA p21 ph8 d38):f3)
  }
  
  p19:gp2       	     
  d16			     
  10u pl12:f2 pl16:f3 
  
  4u cpd2:f2
  d34 
    
  go=2 ph31 cpd3:f3
  d11 do:f2 do:f3 mc #0 to 2 
     F1PH(ip5, id0 & id10) 

4u BLKGRAD
TAU1
exit



ph1 =0
ph2 =1
ph5 =0 2
ph7 =0
ph8 =2

ph31=0 2


;pl1 : f1 channel - power level for pulse (default)
;pl12: f2 channel - power level for pulse (default)
;pl16: f3 channel - power level for CPD/BB decoupling
;sp31: f1 channel - power level for shaped 90 degree pulse
;spnam31: Pc9_4_90.1000 with a consin-modulation 
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p16: homospoil/gradient pulse                         [1 msec]
;p19: homospoil/gradient pulse                         [0.3 msec]
;p41: f1 channel -   90 degree shaped pulse
;d0 : incremented delay (F1 in 3D)                     [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d2 : 1/((2J)XH)
;d8 : mixing time
;d11: delay for disk I/O                               [30 msec]
;d13: short delay                                      [4 usec]
;d16: delay for homospoil/gradient recovery
;d17: fixed delay
;d18: run time delay given by the pulse sequence
;d19: delay for binomial water suppression
;     d19 = (1/(2*d)), d = distance of next null (in Hz)
;d32: run time delay given by the pulse sequence
;d33: run time delay given by the pulse sequence
;d34: run time delay given by the pulse sequence
;d35: run time delay given by the pulse sequence
;d36: run time delay given by the pulse sequence
;d37: run time delay given by the pulse sequence
;d38: run time delay given by the pulse sequence
;cnst3: flipping angle [~120]
;cnst8: 15N spectral width in Hz [=36*sfo3]
;inf1: 1/SW(H) = 2 * DW(H)
;in0: 1/(2 * SW(H)) = DW(H)
;in20: 1s/(2 * cnst1) = DW(H)
;l1:   maximum TD1 value allowable
;NS: 2 * n
;DS: 2*n
;td1: number of experiments in F1
;FnMODE: States-TPPI (or TPPI) in F1
;cpd2: decoupling according to sequence defined by cpdprg2 
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;cpdprg2: f2 channel [bi_garp_2pl]
;cpd3: decoupling according to sequence defined by cpdprg3 
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence
;cpdprg3: f3 channel [bi_garp_2pl.2]


;for z-only gradients:
;gpz1: 20%
;gpz2: 60%

;use gradient files:   
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.50
