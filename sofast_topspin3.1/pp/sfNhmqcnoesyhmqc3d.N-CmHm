;sfNhmqcnoesyhmqc3d.N-CmHm: 3D HMQC-NOESY-HMQC for N(F1)-Cm(F2)Hm(F3)
;H-N(t1)-H -NOE -H-C(t2)-H(t3): N(t1) for NH, C(t2) and H(t3) are for methyl
;Youlin Xia on 05/05/2016

;3D SOFAST-NOESY delivers high sensitivity and resolution to protein NMR.
;P. Rossi, Y. Xia, N. Khanra, G. Veglia, and C.G. Kalodimos	 
;Accepted by JBNMR (2016)

prosol relations=<triple>

#include <Avance.incl>
#include <Delay.incl>
#include <Grad.incl>

"d11=30m"


"in0 =inf1/2"
"in10=inf2/2"
"d0=0u"
"d10=0u"


"d14=1s/(cnst4*2)"	;cnst4=125
"d15=1s/(cnst5*2)"	;cnst5=95

"p41=7.2/(cnst2*bf1/1000000)" /*  PC9  pulse length  */
"p42=4.875/(cnst2*bf1/1000000)" /* REBURP pulse length  */
"cnst3=90"

"spw25=plw1*(pow((p1/p41)/0.125,2))"
	;"sp25=pl1-20*log((p1/p41)/0.125)/log(10)" /* PC9  power level  */
"spoff25=bf1*(cnst7/1000000)-o1"  /*  PC9  offset */

"spw26=plw1*(pow((p1*2/p42)/0.0798,2))"
	;"sp26=pl1-20*log((p1*2/p42)/0.0798)/log(10)"   /* REBURP power level  */
"spoff26=spoff25"  		/*  PC9  offset */

"spw27=plw1*(pow((p1*(cnst3/90)/p41)/0.125,2))"
	;"sp27=pl1-20*log((cnst3/90.0)*(p1/p41)/0.125)/log(10)" /* PC9  power level  */
"spoff27=bf1*(cnst1/1000000)-o1"  /*  PC9  offset */

"spw28=plw1*(pow((p1*2/p42)/0.0798,2))"
	;"sp28=pl1-20*log((p1*2/p42)/0.0798)/log(10)"   /* REBURP power level  */
"spoff28=spoff27"  		/*  PC9  offset */

"plw20=0"

"DELTA1=d14-p16-d16-p41*0.5"
"DELTA2=p41*0.5-de-4u"
"DELTA3=d15-p16-d16-p41*0.5"
"DELTA4=p41*0.5"

"TAU=d8-p3-p16-0.5m-p41" 	


aqseq 321


1 d11 ze
2 d11 do:f2
3 d1 pl0:f1 pl20:f2 pl3:f3

  10u UNBLKGRAD
  
  (p21 ph1):f3
  p16:gp3
  200u

  "d20 = d0*2 - p21*0.637*2"
  if "d20 < 0"
   { 
   "d20 = 0"
   }
  
  "d21 = d10*2 - p3*0.637*2"
  if "d21 < 0"
   { 
   "d21 = 0"
   }

  ;HMQC1
  (p41:sp25 ph1):f1
  p16:gp1
  d16 

  (center (p42:sp26 ph5):f1 (p8:sp13 ph1):f2 (DELTA3 p21 ph3 d20 p21 ph4 DELTA3):f3 )

  p16:gp1
  d16  pl2:f2
  
  (p41:sp25 ph1):f1
  2u
  (p41:sp27 ph1):f1
  ;DELTA4 pl1:f1
  ;(p1 ph1)
  ;4u pl0:f1
  ;(p11:sp1 ph9:r):f1

  
  ;mixing
  TAU
  (p3 ph1):f2
  p16:gp3
  0.5m
    
  ;HMQC2
  
  (p41:sp27 ph1):f1
  p16:gp1
  d16  

  (center (p42:sp28 ph8):f1  (DELTA1 p3 ph6 d21 p3 ph7 DELTA1):f2 )


  DELTA2
  p16:gp1
  d16 pl12:f2
  4u BLKGRAD
    
  go=2 ph31 cpd2:f2
  d11 do:f2 mc #0 to 2 
     ;for topspin2.0
     F1PH(rd10 & rp6 & ip3, id0) 
     F2PH(ip6, id10)

     ;for topspin3.0
     ;F1PH(calph(ph3, +90), caldel(d0, +in0)) 
     ;F2PH(calph(ph6, +90), caldel(d10, +in10)) 
exit


ph1 =0
ph2 =1
ph3 =0 2
ph4 =0 0 0 0 2 2 2 2
ph5 =0 
ph6 =0 0 2 2
ph7 =0
ph8 =0
ph9 =2
ph31=0 2 2 0 2 0 0 2



;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;sp25: f1 channel - power level for shaped  90 degree pulse
;sp26: f1 channel - power level for shaped 180 degree pulse
;sp27: f1 channel - power level for shaped  90 degree pulse
;sp28: f1 channel - power level for shaped 180 degree pulse
;spnam25: Pc9_4_90.1000
;spnam26: Reburp.1000
;spnam27: Pc9_4_90.1000
;spnam28: Reburp.1000
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p16: homospoil/gradient pulse                         [1 msec]
;p41: f1 channel -   90 degree shaped pulse
;p42: f1 channel -  180 degree shaped pulse
;d0 : incremented delay (F1 in 3D)                     [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d2 : 1/((2J)XH)
;d8 : mixing time
;d10: incremented delay (F2 in 3D)                     [3 usec]
;d11: delay for disk I/O                               [30 msec]
;d14: 1/(2JCH)                                         [4.0 msec]
;d16: delay for homospoil/gradient recovery
;d20: run time delay given by the pulse sequence
;d21: run time delay given by the pulse sequence
;cnst1: center of methyl 1H in ppm [0.9]
;cnst2: 1H excitation bandwidth [5]
;cnst3: flipping angle of the 2nd HMQC[110]
;cnst4: 1JCH of aromatic ring [125]
;cnst5: 1JNH [95]
;cnst7: center of NH 1H in ppm [8.5]
;o1p:    center of methyl 1H  chemical shifts [4.7]
;o2p:    center of methyl 13C chemical shifts [16]

;inf1: 1/SW(H) = 2 * DW(H)
;inf2: 1/SW(X) = 2 * DW(X)
;in0: 1/(2 * SW(H)) = DW(H)
;nd0: 2
;in10: 1/(2 * SW(X)) = DW(X)
;nd10: 2
;NS: 8 * n
;DS: 8
;td1: number of experiments in F1
;td2: number of experiments in F2
;FnMODE: States-TPPI (or TPPI) in F1
;FnMODE: States-TPPI (or TPPI) in F2
;cpd2: decoupling according to sequence defined by cpdprg2
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;cpdprg2: f2 channel - bi_garp_2pl


;for z-only gradients:
;gpz1: 55%
;gpz3: 30%

;use gradient files:   
;gpnam1: SMSQ10.100
;gpnam3: SMSQ10.100

